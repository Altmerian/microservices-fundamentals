plugins {
    id 'org.springframework.cloud.contract'
    id 'maven-publish'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.hibernate:hibernate-validator:7.0.4.Final'
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"

    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'

    runtimeOnly 'org.postgresql:postgresql'
}

contracts {
    testFramework = "JUNIT5"
    baseClassForTests = "com.pshakhlovich.microservices_fundamentals.song.controller.SongContractTestBase"
    contractsDslDir = new File("${projectDir}/src/contractTest/resources/contracts/")
}

contractTest {
    useJUnitPlatform()
    finalizedBy(publishToMavenLocal, consumerContractTest)
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    contractTest {
        java {
            srcDir '/src/contractTest/java'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar
            artifact verifierStubsJar

            // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/273
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
        }
    }
}